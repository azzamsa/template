#!/usr/bin/env -S just --justfile

shebang := if os() == 'windows' { 'powershell.exe' } else { '/usr/bin/sh' }

alias d := dev
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    dprint --version || cargo install --locked dprint

    pnpm i
    pnpx husky install

# Clean build output
clean:
    rm -rf build

# Develop the app.
dev:
    pnpm run dev

# Build the app.
build:
    pnpm run build:vite

# Format the codebase.
fmt:
    pnpm run fmt
    dprint fmt --config configs/dprint.json

# Check is the codebase properly formatted.
fmt-check:
    pnpm run fmt:check
    dprint check --config configs/dprint.json

# Lint the codebase.
lint:
    pnpm run lint

# Test the codebase.
test:
    pnpm run test:e2e

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint

# Create a new release. Example `just release v2.2.0`
release version:
    bash scripts/release.sh {{ version }}

# Check dependencies health. Pass `--write` to uppgrade dependencies.
up arg="":
    #!{{ shebang }}
    if [ "{{ arg }}" = "--write" ]; then
        taze --write
    else
        taze
    fi;
