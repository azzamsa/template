#!/usr/bin/env -S just --justfile

alias d := dev
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository
setup:
    git cliff --version || cargo install git-cliff
    cargo-set-version --help || cargo install cargo-edit
    sqlx --version || cargo install sqlx-cli --no-default-features --features postgres,native-tls
    cargo watch --version || cargo install cargo-watch
    cargo outdated --version || cargo install --locked cargo-outdated
    dprint --version || cargo install dprint

dev:
    cargo watch -x clippy -x '+nightly fmt' -x run

# Format the codebase.
fmt:
    cargo +nightly fmt --all
    dprint fmt --config configs/dprint.json

# Check is the codebase properly formatted.
fmt-check:
    cargo +nightly fmt --all -- --check
    dprint check --config configs/dprint.json

# Lint the codebase.
lint:
    cargo clippy --locked --all-targets --all-features

# Check the documentation.
_doc-check:
    cargo doc --all-features --no-deps

# Test the codebase.
test:
    cargo test --all-targets -- --test-threads 1

update-sqlx-schema:
    cargo sqlx prepare -- --lib

check-sqlx-schema:
    cargo sqlx prepare --check -- --lib

# Setup the database schema.
migrate-db:
    sqlx database create
    sqlx migrate run

# reset the database schema.
reset-db:
    sqlx database drop && sqlx database create

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint test update-sqlx-schema

# Check if the repository comply with the rules and ready to be pushed.
check: check-sqlx-schema fmt-check lint test _doc-check

# Create a new release. Example `just release v2.2.0`
release version:
    bash scripts/release.sh {{ version }}

# Check dependencies health.
up:
    cargo +nightly udeps
    cargo outdated --root-deps-only

# Local Variables:
# mode: makefile
# End:
# vim: set ft=make :
