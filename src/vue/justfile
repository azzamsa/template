#!/usr/bin/env -S just --justfile

alias d := dev
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    pnpm i
    pnpx husky install

# Develop the app.
dev:
    pnpm dev

# Build the app.
build:
    pnpm run build-only

# Format the codebase.
fmt:
    dprint fmt --config configs/dprint.json
    pnpm run fmt

# Check is the codebase properly formatted.
fmt-check:
    dprint check --config configs/dprint.json

# Lint the codebase.
lint: fmt-check
    pnpm run lint

test:
    pnpm run test:unit

_test-ci:
    pnpm run test:unit
    # TODO
    # pnpm run test-e2e
    # all the test passed in 'open' mode, but buggy in 'run' mode

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint test build

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint _test-ci build

# Create a new release. Example `just release v2.2.0`
release version:
    bash scripts/release.sh {{ version }}

# Check dependencies health.
up:
    taze
